module eco_compact_util
    use cudafor
    use variable_list, only: P_de, ix1, iy1, dx, dy
    use variable_list, only: d_indices_T, d_isValid_T
    use variable_list, only: nattri, d_isValid

contains
!1--------------------------------------------------------------------
    attributes(global) subroutine prepare_holes(holes,numElements)
        !original:        1 0 1 1 0 0 1 0  d_isValid
        !index(or ii):    1 2 3 4 5 6 7 8  ii
        !prefix sum:      1 1 2 3 3 3 4 4  d_indices
        !ii-d_indices:    0 1 1 1 2 3 3 4
        !N-sum(index):    3 3 2 1 1 1 0 0  d_indices(numElements)-d_indices
        !N-sum(index)+1:  4 4 3 2 2 2 1 1  ii2
        !numElements:     8
        !holes:           2 5 6 8
        !holes index:     1 2 3 4

        implicit none
        ! integer,intent(in):: d_indices(:), d_isValid(:)
        integer,intent(out):: holes(:)
        !dimension is determined by np_active - d_indices(np_active)
        !np_active is numElements
        !holes is a temporary array that just needs dynamic allocation on cpu
        integer:: ii   !global index in the array
        integer,value:: numElements

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x
        if(ii <= numElements .and. d_isValid_T(ii) == 0) then
            holes(ii - d_indices_T(ii)) = ii
        end if
    end subroutine prepare_holes
!2--------------------------------------------------------------------
    attributes(global) subroutine select2send(holes, &
        d_out,offset,hole_size,sumsend)
        !original:        1 0 1 1 0 0 1 0  d_isValid
        !index(or ii):    1 2 3 4 5 6 7 8  ii
        !prefix sum:      1 1 2 3 3 3 4 4  d_indices
        !ii-d_indices:    0 1 1 1 2 3 3 4

        implicit none
        real(8),intent(out):: d_out(:)
        ! real(8),intent(in):: d_in(:,:)
        integer,intent(in):: holes(:)
        integer:: ii, temp1, temp2, temp3
        integer,value:: offset, hole_size, sumsend

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= hole_size) then
            temp1 = (sumsend + ii - 1)*nattri + 1
            temp2 = (sumsend + ii)*nattri
            temp3 = offset + holes(ii)
            d_out(temp1:temp2) = P_de(temp3,:)
        end if

    end subroutine select2send
!3--------------------------------------------------------------------
    attributes(global) subroutine unpack_recv(d_in,offset,sumrecv)
        !original:        1 0 1 1 0 0 1 0  d_isValid
        !index(or ii):    1 2 3 4 5 6 7 8  ii
        !prefix sum:      1 1 2 3 3 3 4 4  d_indices
        !ii-d_indices:    0 1 1 1 2 3 3 4

        implicit none
        real(8),intent(in):: d_in(:)
        ! real(8),intent(out):: d_out(:,:)
        ! integer,intent(in):: holes(:)
        integer:: ii, temp1, temp2, temp3
        integer,value:: offset, sumrecv

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= sumrecv) then
            temp1 = (ii - 1)*nattri + 1
            temp2 = ii*nattri
            temp3 = offset + ii
            P_de(temp3,1:nattri) = d_in(temp1:temp2)
        end if

    end subroutine unpack_recv
!4--------------------------------------------------------------------
    attributes(global) subroutine select2exit(holes,d_out,hole_size)

        implicit none
        ! real(8),intent(in):: d_in(:,:)
        real(8),intent(out):: d_out(:,:)
        integer,intent(in):: holes(:)
        integer:: ii   !global index in the array
        integer,value:: hole_size

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= hole_size) then
            d_out(ii,:) = P_de(holes(ii),:)
        end if

    end subroutine select2exit
!5--------------------------------------------------------------------
    attributes(global) subroutine select2np_active(holes,numElements,hole_size)

        implicit none
        ! real(8),intent(inout):: d_in(:,:)
        integer,intent(in):: holes(:)
        integer,value:: numElements,hole_size
        integer:: ii, temp1, temp2

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= hole_size) then
            temp1 = numElements+ii
            temp2 = holes(ii)
            P_de(temp1,:) = P_de(temp2,:)
        end if

    end subroutine select2np_active
!6--------------------------------------------------------------------
    attributes(global) subroutine compaction_inplace(holes,offset,numElements)
        !original:        1 0 1 1 0 0 1 0  d_isValid
        !index(or ii):    1 2 3 4 5 6 7 8  ii
        !prefix sum:      1 1 2 3 3 3 4 4  d_indices
        !ii-d_indices:    0 1 1 1 2 3 3 4
        !N-sum(index):    3 3 2 1 1 1 0 0  d_indices(numElements)-d_indices
        !N-sum(index)+1:  4 4 3 2 2 2 1 1  ii2
        !numElements:     8
        !holes:           2 5 6 8
        !holes index:     1 2 3 4

        implicit none
        ! real(8),intent(inout):: d_in(:,:)
        integer,intent(in):: holes(:) !,d_indices(:),d_isValid(:)
        !dimension is determined by np_active - d_indices(np_active)
        !np_active is numElements
        !holes is a temporary array that just needs dynamic allocation on cpu
        integer:: ii, ii2, temp1, temp2, tempi   !global index in the array
        integer,value:: offset, numElements

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x
        tempi = d_indices_T(numElements)

        !if(ii <= numElements) &
        !ii2 = d_indices(numElements) - d_indices(ii) + 1
        !if(ii == 1) print *, 'rank',rank,'ii',ii,'ii2',ii2
        !if(ii == numElements) print *, 'rank',rank,'ii',ii,'ii2',ii2

        if(ii <= numElements .and. d_isValid_T(ii) > 0) then
            ii2 = tempi - d_indices_T(ii) + 1
            if (ii2 <= numElements-tempi .and. &
                holes(ii2) <= tempi) then
                temp1 = offset+holes(ii2)
                temp2 = offset+ii
                P_de(temp1,:) = P_de(temp2,:)
            end if
        end if

    end subroutine compaction_inplace
!7--------------------------------------------------------------------
    attributes(global) subroutine prepare_neighbor(d_in,numElements,neighbor)
        !numElements is the current number of elements left in P, i.e., updated np_active

        implicit none
        real(8),intent(in):: d_in(:)
        ! the 8th attribution of P array
        ! integer,intent(out):: d_isValid(:)
        integer,value:: numElements, neighbor
        ! numElements is the length to scan
        ! neighbor is the rank of neighbor
        integer:: ii   !global index in the array

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= numElements) then
            if(idnint(d_in(ii)) == neighbor) then
                d_isValid(ii) = 0
            else
                d_isValid(ii) = 1
            end if
        end if
    end subroutine prepare_neighbor
!8--------------------------------------------------------------------
    attributes(global) subroutine prepare_neigh_all(d_in,numElements,neighbor)
        !numElements is the current number of elements left in P, i.e., updated np_active

        implicit none
        real(8),intent(in):: d_in(:)
        ! the 8th attribution of P array
        ! integer,intent(out):: d_isValid(:)
        integer,value:: numElements
        integer,value:: neighbor
        ! numElements is the length to scan
        ! neighbor is the rank of neighbor
        integer:: ii   !global index in the array

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= numElements) then
            if(idnint(d_in(ii)) /= neighbor) then
                d_isValid(ii) = 0
            else
                d_isValid(ii) = 1
            end if
        end if
    end subroutine prepare_neigh_all
!9--------------------------------------------------------------------
    attributes(global) subroutine connect_recv(numElements,N_recv)
        ! connect the received particles to the bottom of P and
        ! update P(:,8) of the received particles

        implicit none
        ! real(8),intent(inout):: d_in(:,:)
        integer,value:: numElements, N_recv
        integer:: ii   !global index in the array

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= N_recv) then
            P_de(numElements+ii,:) = P_de(numElements+N_recv+ii,:)
        end if
    end subroutine connect_recv

    attributes(global) subroutine local2global(N_inte,N_peri)

        implicit none
        integer,value:: N_inte,N_peri
        integer:: ii

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= N_peri) then
            P_de(N_inte+ii,1) = P_de(N_inte+ii,1) + dble(ix1)*dx
            P_de(N_inte+ii,2) = P_de(N_inte+ii,2) + dble(iy1)*dy
        end if

    end subroutine local2global

    attributes(global) subroutine global2local(N_inte,N_peri)

        implicit none
        integer,value:: N_inte,N_peri
        integer:: ii

        ii = (blockIdx%x - 1) * blockDim%x + threadIdx%x

        if(ii <= N_peri) then
            P_de(N_inte+ii,1) = P_de(N_inte+ii,1) - dble(ix1)*dx
            P_de(N_inte+ii,2) = P_de(N_inte+ii,2) - dble(iy1)*dy
        end if

    end subroutine global2local

end module eco_compact_util