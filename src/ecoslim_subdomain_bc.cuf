module subdomain_bound
    ! subroutine global_xyz
    ! subroutine local_xyz
    ! subroutine read_restarts
    use cudafor

    use variable_list, only: ppx, qqy, rank, buff
    use variable_list, only: nx_c, ny_c, nz_c
    use variable_list, only: dx_c, dy_c, dz
    use variable_list, only: xgmin_c, xgmax_c, ygmin_c, ygmax_c, zgmin_c, zgmax_c
    use variable_list, only: xmin1_c, xmax1_c, ymin1_c, ymax1_c, zmin1_c, zmax1_c
    use variable_list, only: xmin2_c, xmax2_c, ymin2_c, ymax2_c, zmin2_c, zmax2_c
    use variable_list, only: xmin3_c, xmax3_c, ymin3_c, ymax3_c, zmin3_c, zmax3_c
    use variable_list, only: xgmin, xgmax, ygmin, ygmax, zgmin, zgmax
    use variable_list, only: xmin1, xmax1, ymin1, ymax1, zmin1, zmax1
    use variable_list, only: xmin2, xmax2, ymin2, ymax2, zmin2, zmax2
    use variable_list, only: xmin3, xmax3, ymin3, ymax3, zmin3, zmax3
    use variable_list, only: restartf, ranknum
    use variable_list, only: fh1, fh2
    use variable_list, only: ix1_c, iy1_c, nnx1_c, nny1_c, ix2_c, iy2_c, nnx2_c, nny2_c
    use variable_list, only: DEM, DEMname, fname, Pnts
    use variable_list, only: np, nind_c, np_active, pid
    use variable_list, only: P, grid, Zone_de, Zonet_new

contains
    subroutine global_xyz()
        ! get the boundary of the whole domain.
        ! Only effective here for output purpose, after the call of local_xyz
        ! this group of data are updated to local data
        ! for extra GPU, this group of data will be updated when it is activated

        implicit none

        xgmin_c = 0.0d0
        ygmin_c = 0.0d0
        zgmin_c = 0.0d0
        xgmax_c = dble(nx_c)*dx_c
        ygmax_c = dble(ny_c)*dy_c
        zgmax_c = sum(dz)

            #if _TIMING == 1
        write(11,*)
        write(11,*) 'TIMING defined'
            #else
        write(11,*)
        write(11,*) 'TIMING not defined'
            #endif
        write(11,*)
        write(11,*) '## Domain Info'
        write(11,'("xmin:",e12.5," xmax:",e12.5)') xgmin_c, xgmax_c
        write(11,'("ymin:",e12.5," ymax:",e12.5)') ygmin_c, ygmax_c
        write(11,'("zmin:",e12.5," zmax:",e12.5)') zgmin_c, zgmax_c
        write(11,*)

    end subroutine global_xyz

    subroutine local_xyz()

        implicit none

        ! set up subdomain boundaries
        xmin1_c = 0.0d0
        ymin1_c = 0.0d0
        zmin1_c = 0.0d0
        xmax1_c = dble(nnx1_c)*dx_c
        ymax1_c = dble(nny1_c)*dy_c
        zmax1_c = sum(dz)

        xmin2_c = -dble(buff)*dx_c
        ymin2_c = -dble(buff)*dy_c
        zmin2_c = 0.0d0
        xmax2_c = dble(nnx1_c)*dx_c + dble(buff)*dx_c
        ymax2_c = dble(nny1_c)*dy_c + dble(buff)*dy_c
        zmax2_c = sum(dz)

        xmin3_c = dble(buff)*dx_c
        ymin3_c = dble(buff)*dy_c
        zmin3_c = 0.0d0
        xmax3_c = dble(nnx1_c)*dx_c - dble(buff)*dx_c
        ymax3_c = dble(nny1_c)*dy_c - dble(buff)*dy_c
        zmax3_c = sum(dz)

        xgmin_c = xgmin_c - dble(ix1_c)*dx_c
        ygmin_c = ygmin_c - dble(iy1_c)*dy_c
        zgmin_c = 0.0d0
        xgmax_c = xgmax_c - dble(ix1_c)*dx_c
        ygmax_c = ygmax_c - dble(iy1_c)*dy_c
        zgmax_c = sum(dz)

        xgmin = xgmin_c; xgmax = xgmax_c; ygmin = ygmin_c
        ygmax = ygmax_c; zgmin = zgmin_c; zgmax = zgmax_c
        xmin1 = xmin1_c; xmax1 = xmax1_c; ymin1 = ymin1_c
        ymax1 = ymax1_c; zmin1 = zmin1_c; zmax1 = zmax1_c
        xmin2 = xmin2_c; xmax2 = xmax2_c; ymin2 = ymin2_c
        ymax2 = ymax2_c; zmin2 = zmin2_c; zmax2 = zmax2_c
        xmin3 = xmin3_c; xmax3 = xmax3_c; ymin3 = ymin3_c
        ymax3 = ymax3_c; zmin3 = zmin3_c; zmax3 = zmax3_c

    end subroutine local_xyz

    subroutine read_restarts ()
        use mpi
        implicit none

        integer:: ierr
        character(200):: message

        if (rank /= ppx*qqy) then

            write(message,'(a,a,i3.3,a,a)') 'Reading particle restart File:', &
            'Particle_restart.',rank,'.bin',new_line(' ')
            call MPI_FILE_WRITE(fh1, trim(message), len(trim(message)), &
                                MPI_CHARACTER, MPI_STATUS_IGNORE, ierr)

            call MPI_FILE_OPEN(MPI_COMM_SELF,restartf,MPI_MODE_RDONLY, &
                                MPI_INFO_NULL,fh2,ierr)

            call MPI_FILE_READ(fh2,np_active,1,MPI_INTEGER,MPI_STATUS_IGNORE,ierr)
            call MPI_FILE_READ(fh2,pid,1,MPI_INTEGER,MPI_STATUS_IGNORE,ierr)

            if (np_active < np) then   ! check if we have particles left
                call MPI_FILE_READ(fh2,P(1:np_active,1:nind_c*2+17),np_active*(nind_c*2+17), &
                                    MPI_DOUBLE_PRECISION,MPI_STATUS_IGNORE,ierr)
                call MPI_FILE_CLOSE(fh2, ierr)

                write(message,'(a,i10.10,a)') 'RESTART np_active:',np_active,new_line(' ')
                call MPI_FILE_WRITE(fh1, trim(message), len(trim(message)), &
                                    MPI_CHARACTER, MPI_STATUS_IGNORE, ierr)
                write(message,'(a,i10.10,a)') 'RESTART pid:',pid,new_line(' ')
                call MPI_FILE_WRITE(fh1, trim(message), len(trim(message)), &
                                    MPI_CHARACTER, MPI_STATUS_IGNORE, ierr)
            else
                call MPI_FILE_CLOSE(fh2, ierr)
                write(message,'(A,A)') ' **Warning restart IC input but no paricles left',new_line(' ')
                call MPI_FILE_WRITE(fh1, trim(message), len(trim(message)), &
                                    MPI_CHARACTER, MPI_STATUS_IGNORE, ierr)
                write(message,'(A,A)') ' **Exiting code *not* (over)writing restart',new_line(' ')
                call MPI_FILE_WRITE(fh1, trim(message), len(trim(message)), &
                                    MPI_CHARACTER, MPI_STATUS_IGNORE, ierr)
                stop
            end if

            ! read topology
            open(17,file='topology_restart.'//trim(adjustl(ranknum)),FORM='unformatted',access='stream')
                read(17) ix1_c, iy1_c, nnx1_c, nny1_c, ix2_c, iy2_c, nnx2_c, nny2_c
            close(17)  ! Topology
        else
            !open(19,file='manager_grid_zonet_new', FORM='unformatted',access='stream')
            !    read(19) grid
            !    read(19) Zonet_new
            !close(19)  ! manager
        end if
    end subroutine read_restarts

end module subdomain_bound